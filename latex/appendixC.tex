\begin{figure}[h]
\begin{lstlisting}[language=BNF,basicstyle=\linespread{0.8}\ttfamily,
                   captionpos=t,caption=
   {Verbatim display of the grammar file that is used to generate the parser for
    CAnDL.
    The file is in a custom version of Backusâ€“Naur form.
    In the parse tree, all expressions that start with ``{@}'' are
    automatically expanded.
    The character ``\#'' marks the top-level language construct.
    Any expression that does not ultimately become part of this construct
    constitutes a syntax error.\parfillskip=0pt}]
# ::= <specification> | <export>

export ::= Export

specification ::= Constraint <s> <@formula> End

@formula ::= <atom> | <conjunction> | <disjunction>
 | <rename> | <if> | <default> | <collect> | <@nested>

@nested ::= '(' <@formula> ')'

conjunction ::= '(' <@formula> and <@formula>
                             { and <@formula> } ')'

disjunction ::= '(' <@formula> or <@formula>
                             { or <@formula> } ')'

rename ::= ( <conRange> | <disRange> | <include> | <for> )
           [ with <@variable> as <@variable>
           {  and <@variable> as <@variable> } ]
           [ at <@variable> ]

include ::= inherits <s> [ '(' <s> = <@index>
                         { ',' <s> = <@index> } ')' ]

for      ::= <@formula> for      <s> = <@index>
conRange ::= <@formula> for all  <s> = <@index> '..' <@index>
disRange ::= <@formula> for some <s> = <@index> '..' <@index>

if ::= if <@index> = <@index> then <@formula>
                              else <@formula> endif

default ::= <@formula> for <s> = <@index>
            if not otherwise specified

\end{lstlisting}
\end{figure}

\begin{figure}[p]
\begin{lstlisting}[language=BNF,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=36]
collect ::= collect <s> <n> <@formula>

atom ::= <IntegerType> | <FloatType>
 | <VectorType> | <PointerType>
 | <Unused> | <IntZero> | <FloatZero>
 | <Constant> | <Preexecution> | <Argument> | <Instruction>
 | <Same> | <Distinct>
 | <DFGEdge> | <CFGEdge> | <CDGEdge> | <PDGEdge>
 | <FirstOperand> | <SecondOperand>
 | <ThirdOperand> | <FourthOperand>
 | <FirstSuccessor> | <SecondSuccessor>
 | <ThirdSuccessor> | <FourthSuccessor>
 | <Dominate> | <DominateStrict>
 | <Postdom> | <PostdomStrict> | <Blocked>
 | <IncomingValue> | <FunctionAttribute>
 | <Opcode> | <GeneralizedSame>
 | <GeneralizedDominance> | <NotNumericConstant> | <Block>

IntegerType ::= <@variable> is an integer
FloatType   ::= <@variable> is a float
VectorType  ::= <@variable> is a vector
PointerType ::= <@variable> is a pointer

Unused ::= <@variable> is unused
Opcode ::= <@variable> is <s> instruction

IntZero   ::= <@variable> is integer zero
FloatZero ::= <@variable> is floating point zero

Constant           ::= <@variable> is a constant
Preexecution       ::= <@variable> is preexecution
Argument           ::= <@variable> is an argument
Instruction        ::= <@variable> is instruction
NotNumericConstant ::= <@variable> is not a numeric constant

Same     ::= <@variable> is the same as <@variable>
Distinct ::= <@variable> is not the same as <@variable>
Block    ::= <@variable> spans block to <@variable>

DFGEdge ::= <@variable> has data flow to <@variable>
CFGEdge ::= <@variable> has control flow to <@variable>
CDGEdge ::= <@variable> has control dominance to <@variable>
PDGEdge ::= <@variable> has dependence edge to <@variable>

FirstOperand  ::= <@variable> is first
                  argument of <@variable>
SecondOperand ::= <@variable> is second
                  argument of <@variable>
ThirdOperand  ::= <@variable> is third
                   argument of <@variable>
FourthOperand ::= <@variable> is fourth
                   argument of <@variable>
\end{lstlisting}
\end{figure}

\begin{figure}[p]
\begin{lstlisting}[language=BNF,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=89]
FirstSuccessor  ::= <@variable> is first
                    successor of <@variable>
SecondSuccessor ::= <@variable> is second
                    successor of <@variable>
ThirdSuccessor  ::= <@variable> is third
                    successor of <@variable>
FourthSuccessor ::= <@variable> is fourth
                    successor of <@variable>

Dominate       ::= <@variable> control flow
                   dominates <@variable>
Postdom        ::= <@variable> control flow
                   post dominates <@variable>
DominateStrict ::= <@variable> strictly control flow
                   dominates <@variable>
PostdomStrict  ::= <@variable> strictly control flow
                   post dominates <@variable>

IncomingValue ::= <@variable> reaches phi node
                  <@variable> from <@variable>

Blocked ::= all control flow from <@variable> to <@variable>
            passes through <@variable>

FunctionAttribute ::= <@variable> has attribute pure

GeneralizedSame ::= <@variable> is the same set as <@variable>

GeneralizedDominance ::= all flow from <@variable> or any origin
                         to any of <@variable> passes through
                         at least one of <@variable>

@variable ::= <slottuple> | '{' <@openslot> '}'

slottuple ::= '{' <@openslot> ',' <@openslot>
                            { ',' <@openslot> } '}'

@openslot ::= <slotmember> | <slotindex> | <slotrange>
            | <slottuple>  | <slotbase>

slotbase   ::= <s>
slotmember ::= <@openslot> '.' <s>
slotindex  ::= <@openslot> '[' <@index> ']'
slotrange  ::= <@openslot> '[' <@index> '..' <@index> ']'

@index ::= <basevar> | <baseconst>
 | <addvar> | <addconst> | <subvar> | <subconst>

basevar   ::= <s>
baseconst ::= <n>
addvar    ::= <@index> '+' <s>
addconst  ::= <@index> '+' <n>
subvar    ::= <@index> '-' <s>
subconst  ::= <@index> '-' <n>
\end{lstlisting}
\end{figure}