\begin{figure}[h]
\begin{lstlisting}[language=CAnDL,basicstyle=\linespread{0.8}\ttfamily,
                   captionpos=t,caption=
   {Constraint specification in CAnDL of Scalar Control Parts (SCoPs):
    These regular loop nests enable polyhedral transformations to be applied,
    due to their structured control flow, side effect free function calls and
    the well-behaved memory access that is affine in loop iterators.
    The involved CAnDL code is displayed, including relevant parts of the CAnDL
    standard library.
    \parfillskip=0pt}]
Constraint SCoP
( include For @ {loop}
∧ include StructuredControlFlow({loop}->{scope}) @ {control}
∧ {inputs[0]} = {loop.iterator}
∧ {inputs[i]} = {control.loop[i-1].iterator} foreach i=1..10
∧ include AffineControlFlow({loop}->{scope},
                          {inputs}->{inputs}) @ {control}
∧ include AffineMemAccesses({loop}->{scope},
                          {inputs}->{inputs}) @ {accesses}
∧ include SideEffectFreeCalls({loop}->{scope}) @ {effects})
End

Constraint StructuredControlFlow
( collect i 20 ( opcode{branch[i].value} = branch
               ∧ {branch[i].target1} =
                     {branch[i].value}.successors[0]
               ∧ {branch[i].target2} =
                     {branch[i].value}.successors[1]
               ∧ include ScopeValue({scope}->{scope},
                          {branch[i].value}->{value}))
∧ collect i 10 ( include For @ {loop[i]}
               ∧ domination({scope.begin},
                          {loop[i].begin})
               ∧ strict_post_domination({scope.end},
                                      {loop[i].end}))
∧ collect i  10 ( include IfBlock @ {ifblock[i]}
               ∧ domination({scope.begin},
                       {ifblock[i].precursor})
               ∧ strict_post_domination({scope.end},
                                   {ifblock[i].successor}))
∧ {loop[0..10].end,ifblock[0..10].precursor}
      is the same set as {branch[0..20].value})
End
\end{lstlisting}
\end{figure}
\begin{figure}[p]
\begin{lstlisting}[language=CAnDL,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=35]
Constraint AffineControlFlow
( include AffineCalc[M=10,N=1](
                           {scope}->{scope},
              {loop[i].iter_begin}->{value},
                          {inputs}->{input})
                @ {forloop_affine_begin[i]} foreach i=0..10
∧ include AffineCalc[M=10,N=1](
                           {scope}->{scope},
                {loop[i].iter_end}->{value},
                          {inputs}->{input})
                  @ {forloop_affine_end[i]} foreach i=0..10
∧ include AffineCalc[M=10,N=1](
                           {scope}->{scope},
         {ifblock[i].compare_left}->{value},
                          {inputs}->{input})
                 @ {ifblock_affine_left[i]} foreach i=0..10
∧ include AffineCalc[M=10,N=1](
                           {scope}->{scope},
        {ifblock[i].compare_right}->{value},
                          {inputs}->{input})
                @ {ifblock_affine_right[i]} foreach i=0..10)
End

Constraint AffineMemAccesses
( collect x 20 ( include MemoryAccess({scope}->{scope})
                                      @ {newaccess[x]}
               ∧ opcode{newaccess[x].pointer} = gep
               ∧ domination({scope.begin},
                     {newaccess[x].pointer})
               ∧ {newaffine[x].value} =
                     {newaccess[x].pointer}.args[1])
∧ collect x 20 ( include MemoryAccess({scope}->{scope})
                                      @ {newaccess[x]}
               ∧ opcode{newaccess[x].pointer} = gep
               ∧ domination({scope.begin},
                     {newaccess[x].pointer})
               ∧ {newaffine[x].value} =
                     {newaccess[x].pointer}.args[1]
               ∧ include AffineCalc[M=10,N=6](
                                          {scope}->{scope},
                                         {inputs}->{input})
                                          @ {newaffine[x]}))
End

Constraint SideEffectFreeCalls
( collect i 20 ( opcode{callsite[i]} = call
               ∧ include ScopeValue({callsite[i]}->{value}))
∧ collect i 20 ( opcode{callsite[i]} = call
               ∧ include ScopeValue({callsite[i]}->{value})
               ∧ {function[i]} = {callsite[i]}.args[0]
               ∧ {function[i]} has attribute pure))
End
\end{lstlisting}
\end{figure}
\begin{figure}[p]
\begin{lstlisting}[language=CAnDL,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=88]
Constraint For
( include Loop
∧ {increment} -> {body.end} Φ {iterator}
∧ {increment} ∈ {comparison}.args
∧ opcode{comparison} = icmp
∧ {comparison} ∈ {end}.args
∧ {increment} ∈ {iterator}.args
∧ opcode{increment} = add
∧ {iterator} ∈ {increment}.args
∧ {iter_end} ∈ {comparison}.args
∧ include LocalConst({begin}->{scope.begin},
                  {iter_end}->{value})
∧ {iter_begin} ∈ {iterator}.args
∧ include LocalConst({begin}->{scope.begin},
                {iter_begin}->{value})
∧ {iter_step} ∈ {increment}.args
∧ include LocalConst({begin}->{scope.begin},
                 {iter_step}->{value}))
End

Constraint Loop
( include SESE
∧ {begin} ∈ {end}.successors)
End

Constraint SESE
( opcode{precursor} = branch
∧ {begin} ∈ {precursor}.successors
∧ opcode{end} = branch
∧ {successor} ∈ {end}.successors
∧ domination({begin}, {end})
∧ post_domination({end}, {begin})
∧ strict_domination({precursor}, {begin})
∧ strict_post_domination({successor}, {end})
∧ all control flow from {begin} to {precursor}
         passes through {end}
∧ all control flow from {successor} to {end}
         passes through {begin})
End

Constraint IfBlock
( include PotentialSESE({truebegin}->{begin},
                          {trueend}->{end})
∧ {truebegin} = {precursor}.successors[0]
∧ {falsebegin} = {precursor}.successors[1]
∧ include PotentialSESE({falsebegin}->{begin},
                          {falseend}->{end})
∧ {trueend} ≠ {falseend}
∧ {condition} = {precursor}.args[0]
∧ opcode{condition} = icmp
∧ {compare_left} = {condition}.args[0]
∧ {compare_right} = {condition}.args[1])
End
\end{lstlisting}
\end{figure}
\begin{figure}[p]
\begin{lstlisting}[language=CAnDL,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=142]
Constraint AffineCalc
if N=0 then
( ( ( include LocalConst
    ∨ ( {value} is unused
      ∧ ir_type{scope.begin} = instruction))
  ∧ ( data_type{input[j]} = integer
    ∨ {input[j]} is unused) foreach j=0..M)
∨ ( ( {value} = {input[i]}
    ∧ ir_type{scope.begin} = instruction
    ∧ ( data_type{input[j]} = integer
      ∨ {input[j]} is unused) foreach j=0..M)
                              forany  i=0..M))
else
( ( ( include AffineCalc[M=M,N=0]
    ∧ {l.value} is unused
    ∧ {r.value} is unused)
  ∨ ( ( ( ( opcode{value} = add
          ∨ opcode{value} = sub)
        ∧ {l.value} = {value}.args[0]
        ∧ {r.value} = {value}.args[1])
      ∨ ( opcode{value} = select
        ∧ {l.value} = {value}.args[1]
        ∧ {r.value} = {value}.args[2])
      ∨ ( opcode{value} = mul
        ∧ include ArgumentsPermuted({l.value}->{src1},
                                    {r.value}->{src2},
                                      {value}->{dst})
        ∧ ir_type{l.value} = preexecution
        ∧ ir_type{r.value} = instruction))
    ∧ ir_type{scope.begin} = instruction
    ∧ domination({scope.begin}, {value})
    ∧ ( data_type{input[j]} = integer
      ∨ {input[j]} is unused) foreach j=0..M))
∧ include AffineCalc[M=M,N=N-1]({input}->{input},
                                {scope}->{scope}) @ {l}
∧ include AffineCalc[M=M,N=N-1]({input}->{input},
                                {scope}->{scope}) @ {r})
endif
End

Constraint ArgumentsPermuted
( ( {src1} = {dst}.args[0]
  ∧ {src2} = {dst}.args[1])
∨ ( {src2} = {dst}.args[0]
  ∧ {src1} = {dst}.args[1]))
End

Constraint LocalConst
( ( ir_type{scope.begin} = instruction
  ∧ ir_type{value} = preexecution)
∨ strict_domination({value}, {scope.begin}))
End
\end{lstlisting}
\end{figure}
\begin{figure}[p]
\begin{lstlisting}[language=CAnDL,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=195]
Constraint MemoryAccess
( ( ( opcode{access} = store
    ∧ {pointer} = {access}.args[1])
  ∨ ( opcode{access} = load
    ∧ {pointer} = {access}.args[0]))
∧ domination({scope.begin}, {access})
∧ post_domination({scope.end}, {access}))
End

Constraint ScopeValue
( domination({scope.begin}, {value})
∧ strict_post_domination({scope.end}, {value}))
End

Constraint PotentialSESE
( opcode{precursor} = branch
∧ {begin} ∈ {precursor}.successors
∧ opcode{end} = branch
∧ {successor} ∈ {end}.successors
∧ ( ( domination({begin}, {end})
    ∧ post_domination({end}, {begin})
    ∧ all control flow from {begin} to {precursor}
                        passes through {end}
    ∧ all control flow from {successor} to {end}
                            passes through {begin})
  ∨ ( {begin} = {end}
    ∧ {begin} ∈ {precursor}.successors
    ∧ {successor} ∈ {end}.successors)
  ∨ ( {precursor} = {end}
    ∧ {begin} = {successor})))
End
\end{lstlisting}
\end{figure}
