\begin{lstlisting}[language=CAnDL]
Constraint SCoP
( include For
([$\tt\land$]) include StructuredControlFlowPart({body.begin}->{begin},
                                    {body.end}  ->{end})
                                  @ {constraint_control}
([$\tt\land$]) include SideEffectFreePart({begin}->{begin},
                             {end}  ->{end})
                           @ {constraint_effects}
([$\tt\land$]) {constraint_memory.input[0]}   = {iterator}
([$\tt\land$]) {constraint_memory.input[i+1]} =
    {constraint_control.loop[i].iterator} foreach i=0..7
([$\tt\land$]) include AffineMemoryAccessPart({begin}->{begin},
                                 {end}  ->{end})
                               @ {constraint_memory}
([$\tt\land$]) include AffineControlFlowPart({body.begin}->{begin},
                                  {constraint_memory.input}->{input})
                                @ {constraint_control}
)
End

Constraint StructuredControlFlowPart
( collect i 16 ( opcode{branch[i].value} = branch
               ([$\tt\land$]) {branch[i].dummy} = {branch[i].value}.successors[1]
               ([$\tt\land$]) include LocalValue({branch[i].value}->{value}))
([$\tt\land$]) collect i  8 ( include For @ {loop[i]}
               ([$\tt\land$]) domination({begin}, {loop[i].begin})
               ([$\tt\land$]) post_domination({end}, {loop[i].end}))
([$\tt\land$]) collect i  8 ( include IfBlock @ {conditional[i]}
               ([$\tt\land$]) domination({begin}, {conditional[i].precursor})
               ([$\tt\land$]) post_domination({end}, {conditional[i].successor}))
([$\tt\land$]) {loop[0..8].end,conditional[0..8].precursor} is the same set as {branch[0..16].value})
End

Constraint SideEffectFreePart
( collect i 32 ( include LocalFunctionCall({begin}->{begin},{end}->{end}) @ {call[i]}) 
([$\tt\land$]) collect i 32 ( include LocalFunctionCall({begin}->{begin},{end}->{end}) @ {call[i]}
               ([$\tt\land$]) {call[i].function} has attribute pure))
End

Constraint AffineMemoryAccessPart
( collect x 24 ( include MemoryAccess({begin}->{begin},{end}->{end}) @ {newaccess[x]})
([$\tt\land$]) collect x 24 ( include MemoryAccess({begin}->{begin},{end}->{end}) @ {newaccess[x]}
               ([$\tt\land$]) ( ( opcode{newaccess[x].pointer} = gep
                   ([$\tt\land$]) domination({begin}, {newaccess[x].pointer})
                   ([$\tt\land$]) {newaffine[x].value} = {newaccess[x].pointer}.args[1]
                   ([$\tt\land$]) ir_type{begin} = instruction)
                 ([$\tt\lor$]) ( include LocalConst({newaccess[x].pointer}->{value})
                   ([$\tt\land$]) {newaffine[x].value} is unused))
               ([$\tt\land$]) include AffineCalculation[M=8,N=6]({input}->{input},{begin}->{begin}) @ {newaffine[x]}))
End

Constraint AffineControlFlowPart
( include AffineCalculation[M=8,N=1]({conditional[i].compare_left}->{value},
                                     {input}->{input},{begin}->{begin})@{conditional_affine_left[i]} foreach i=0..8
([$\tt\land$]) include AffineCalculation[M=8,N=1]({conditional[i].compare_right}->{value},
                                     {input}->{input},{begin}->{begin})@{conditional_affine_right[i]} foreach i=0..8
([$\tt\land$]) include AffineCalculation[M=8,N=1]({loop[i].iter_begin}->{value},
                                     {input}->{input},{begin}->{begin})@{loop_affine_begin[i]} foreach i=0..8
([$\tt\land$]) include AffineCalculation[M=8,N=1]({loop[i].iter_end}->{value},
                                     {input}->{input},{begin}->{begin})@{loop_affine_end[i]} foreach i=0..8)
End

Constraint For
( include Loop
([$\tt\land$]) {increment} reaches phi node {iterator} from {body.end}
([$\tt\land$]) {increment} ([$\tt \in$]) {comparison}.args
([$\tt\land$]) opcode{comparison} = icmp
([$\tt\land$]) {comparison} ([$\tt \in$]) {end}.args
([$\tt\land$]) {increment} ([$\tt \in$]) {iterator}.args
([$\tt\land$]) opcode{increment} = add
([$\tt\land$]) {iterator} ([$\tt \in$]) {increment}.args
([$\tt\land$]) {iter_end} ([$\tt \in$]) {comparison}.args
([$\tt\land$]) include LocalConst({iter_end}->{value},{begin}->{begin})
([$\tt\land$]) {iter_begin} ([$\tt \in$]) {iterator}.args
([$\tt\land$]) include LocalConst({iter_begin}->{value},{begin}->{begin})
([$\tt\land$]) {iter_step} ([$\tt \in$]) {increment}.args
([$\tt\land$]) include LocalConst({iter_step}->{value},{begin}->{begin}))
End

Constraint MemoryAccess
( ( ( opcode{access} = store
    ([$\tt\land$]) {pointer} = {access}.args[1])
  ([$\tt\lor$]) ( opcode{access} = load
    ([$\tt\land$]) {pointer} = {access}.args[0]))
([$\tt\land$]) domination({begin}, {access})
([$\tt\land$]) post_domination({end}, {access}))
End

Constraint AffineCalculation
if N=0 then
( ( include LocalConst
  ([$\tt\land$]) ( data_type{input[j]} = integer
    ([$\tt\lor$]) {input[j]} is unused) foreach j=0..M)
([$\tt\lor$]) ( ( {value} = {input[i]}
    ([$\tt\land$]) ir_type{begin} = instruction
    ([$\tt\land$]) ( data_type{input[j]} = integer
      ([$\tt\lor$]) {input[j]} is unused) foreach j=0..M) forany i=0..M))
else
( ( ( include AffineCalculation[M=M,N=0]
    ([$\tt\land$]) {l.value} is unused
    ([$\tt\land$]) {r.value} is unused)
  ([$\tt\lor$]) ( ( ( ( opcode{value} = add
          ([$\tt\lor$]) opcode{value} = sub)
        ([$\tt\land$]) {l.value} = {value}.args[0]
        ([$\tt\land$]) {r.value} = {value}.args[1])
      ([$\tt\lor$]) ( opcode{value} = select
        ([$\tt\land$]) {l.value} = {value}.args[1]
        ([$\tt\land$]) {r.value} = {value}.args[2])
      ([$\tt\lor$]) ( opcode{value} = mul
        ([$\tt\land$]) include ArgumentsPermuted({l.value}->{src1},{r.value}->{src2},{value}->{dst})
        ([$\tt\land$]) ir_type{l.value} = preexecution
        ([$\tt\land$]) ir_type{r.value} = instruction))
    ([$\tt\land$]) ir_type{begin} = instruction
    ([$\tt\land$]) domination({begin}, {value})
    ([$\tt\land$]) ( data_type{input[j]} = integer
      ([$\tt\lor$]) {input[j]} is unused) foreach j=0..M))
([$\tt\land$]) include AffineCalculation[M=M,N=N-1]({input}->{input},{begin}->{begin}) @ {l}
([$\tt\land$]) include AffineCalculation[M=M,N=N-1]({input}->{input},{begin}->{begin}) @ {r})
endif
End

Constraint LocalFunctionCall
( opcode{callsite} = call
([$\tt\land$]) {function} = {callsite}.args[0]
([$\tt\land$]) include LocalValue({callsite}->{value}))
End

Constraint IfBlock
( include PotentialSESE({truebegin}->{begin},{trueend}->{end})
([$\tt\land$]) {truebegin} = {precursor}.successors[0]
([$\tt\land$]) {falsebegin} = {precursor}.successors[1]
([$\tt\land$]) include PotentialSESE({falsebegin}->{begin},{falseend}->{end})
([$\tt\land$]) {trueend} ([$\tt\neq$]) {falseend}
([$\tt\land$]) {condition} = {precursor}.args[0]
([$\tt\land$]) opcode{condition} = icmp
([$\tt\land$]) {compare_left} = {condition}.args[0]
([$\tt\land$]) {compare_right} = {condition}.args[1])
End

Constraint PotentialSESE
( opcode{precursor} = branch
([$\tt\land$]) {precursor} ([$\tt \in$]) {begin}.successors
([$\tt\land$]) opcode{end} = branch
([$\tt\land$]) {end} ([$\tt \in$]) {successor}.successors
([$\tt\land$]) ( ( domination({begin}, {end})
    ([$\tt\land$]) post_domination({end}, {begin})
    ([$\tt\land$]) all control flow from {begin} to {precursor} passes through {end}
    ([$\tt\land$]) all control flow from {successor} to {end} passes through {begin})
  ([$\tt\lor$]) ( {begin} = {end}
    ([$\tt\land$]) {precursor} ([$\tt \in$]) {begin}.successors
    ([$\tt\land$]) {end} ([$\tt \in$]) {successor}.successors)
  ([$\tt\lor$]) ( {precursor} = {end}
    ([$\tt\land$]) {begin} = {successor})))
End

Constraint LocalValue
( domination({begin}, {value})
([$\tt\land$]) post_domination({end}, {value}))
End

Constraint Distributive
( opcode{value} = add
([$\tt\land$]) {sum1.value} = {value}.args[0]
([$\tt\land$]) {sum2.value} = {value}.args[1]
([$\tt\land$]) include SumChain @ {sum1}
([$\tt\land$]) {product1.value} = {sum1.last_factor}
([$\tt\land$]) include MulChain @ {product1}
([$\tt\land$]) {product1.last_factor} = {product2.last_factor}
([$\tt\land$]) include SumChain @ {sum2}
([$\tt\land$]) {product2.value} = {sum2.last_factor}
([$\tt\land$]) include MulChain @ {product2})
End

Constraint SumChain
( data_type{value} = integer
([$\tt\land$]) {value} = {results[0]}
([$\tt\land$]) ( ( opcode{results[i]} = add
    ([$\tt\land$]) include ArgumentsPermuted({results[i]}->{dst},{results[i+1]}->{src1},{factors[i]}->{src2})
    ([$\tt\land$]) data_type{last_factor} = integer)
  ([$\tt\lor$]) ( ( ( data_type{results[i]} = integer
        ([$\tt\land$]) {last_factor} = {factors[i]})
      ([$\tt\lor$]) ( {results[i]} is unused
        ([$\tt\land$]) {factors[i]} is unused
        ([$\tt\land$]) data_type{last_factor} = integer))
    ([$\tt\land$]) {results[i]} = {factors[i]}
    ([$\tt\land$]) {results[i+1]} is unused)) foreach i=0..N
([$\tt\land$]) {factors[N]} is unused)
for N=4 if not otherwise specified
End

Constraint MulChain
( data_type{value} = integer
([$\tt\land$]) {value} = {results[0]}
([$\tt\land$]) ( ( opcode{results[i]} = mul
    ([$\tt\land$]) include ArgumentsPermuted({results[i]}->{dst},{results[i+1]}->{src1},{factors[i]}->{src2})
    ([$\tt\land$]) data_type{last_factor} = integer)
  ([$\tt\lor$]) ( ( ( data_type{results[i]} = integer
        ([$\tt\land$]) {last_factor} = {factors[i]})
      ([$\tt\lor$]) ( {results[i]} is unused
        ([$\tt\land$]) {factors[i]} is unused
        ([$\tt\land$]) data_type{last_factor} = integer))
    ([$\tt\land$]) {results[i]} = {factors[i]}
    ([$\tt\land$]) {results[i+1]} is unused)) foreach i=0..N
([$\tt\land$]) {factors[N]} is unused)
for N=4 if not otherwise specified
End

Constraint ArgumentsPermuted
( ( {src1} = {dst}.args[0]
  ([$\tt\land$]) {src2} = {dst}.args[1])
([$\tt\lor$]) ( {src2} = {dst}.args[0]
  ([$\tt\land$]) {src1} = {dst}.args[1]))
End

Constraint HoistSelect
( opcode{select} = select
([$\tt\land$]) {input1} = {select}.args[1]
([$\tt\land$]) {base} = {input1}.args[0]
([$\tt\land$]) {input2} = {select}.args[2]
([$\tt\land$]) {base} = {input2}.args[0]
([$\tt\land$]) opcode{input1} = gep
([$\tt\land$]) opcode{input2} = gep)
End

Constraint ForWithIteratorTest
( include Loop
([$\tt\land$]) {increment} reaches phi node {iterator} from {body.end}
([$\tt\land$]) {iterator} ([$\tt \in$]) {comparison}.args
([$\tt\land$]) opcode{comparison} = icmp
([$\tt\land$]) {comparison} ([$\tt \in$]) {end}.args
([$\tt\land$]) {increment} ([$\tt \in$]) {iterator}.args
([$\tt\land$]) opcode{increment} = add
([$\tt\land$]) {iterator} ([$\tt \in$]) {increment}.args
([$\tt\land$]) {iter_end} ([$\tt \in$]) {comparison}.args
([$\tt\land$]) include LocalConst({iter_end}->{value},{begin}->{begin})
([$\tt\land$]) {iter_begin} ([$\tt \in$]) {iterator}.args
([$\tt\land$]) include LocalConst({iter_begin}->{value},{begin}->{begin})
([$\tt\land$]) {iter_step} ([$\tt \in$]) {increment}.args
([$\tt\land$]) include LocalConst({iter_step}->{value},{begin}->{begin}))
End

Constraint Loop
( include SESE
([$\tt\land$]) {body.begin} = {begin}
([$\tt\land$]) {body.end} = {end}
([$\tt\land$]) {body.precursor} = {precursor}
([$\tt\land$]) {body.successor} = {successor}
([$\tt\land$]) {end} ([$\tt \in$]) {begin}.successors)
End

Constraint InductionVar
{new_ind} reaches phi node {old_ind} from {end}
End

Constraint LocalConst
( ( ir_type{begin} = instruction
  ([$\tt\land$]) ir_type{value} = preexecution)
([$\tt\lor$]) strict_domination({value}, {begin})
([$\tt\lor$]) ( ir_type{begin} = instruction
  ([$\tt\land$]) {value} is unused))
End

Constraint SESE
( opcode{precursor} = branch
([$\tt\land$]) {precursor} ([$\tt \in$]) {begin}.successors
([$\tt\land$]) opcode{end} = branch
([$\tt\land$]) {end} ([$\tt \in$]) {successor}.successors
([$\tt\land$]) domination({begin}, {end})
([$\tt\land$]) post_domination({end}, {begin})
([$\tt\land$]) strict_domination({precursor}, {begin})
([$\tt\land$]) strict_post_domination({successor}, {end})
([$\tt\land$]) all control flow from {begin} to {precursor} passes through {end}
([$\tt\land$]) all control flow from {successor} to {end} passes through {begin})
End
\end{lstlisting}
