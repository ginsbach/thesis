\begin{figure}[h]
\begin{lstlisting}[language=BNF,basicstyle=\linespread{0.8}\ttfamily,
                   captionpos=t,caption=
   {Verbatim display of the grammar file that is used to generate the parser for
    CAnDL.
    The file is in a custom version of Backus–Naur form.
    In the parse tree, all expressions that start with ``{@}'' are
    automatically expanded.
    The character ``\#'' marks the top-level language construct.
    Any expression that does not ultimately become part of this construct
    constitutes a syntax error.\parfillskip=0pt}]
# ::= <specification> | <export>

export ::= Export

specification ::= Constraint <s> <@formula> End

@formula ::= <atom> | <conjunction> | <disjunction>
 | <rename> | <if> | <default> | <collect> | <@nested>

@nested ::= '(' <@formula> ')'

conjunction ::= '(' <@formula> '∧' <@formula>
                             { '∧' <@formula> } ')'

disjunction ::= '(' <@formula> '∨' <@formula>
                             { '∨' <@formula> } ')'

rename ::= ( <conRange> | <disRange> | <include> | <for> )
           [ '(' <@variable> '->' <@variable>
           { ',' <@variable> '->' <@variable> } ')' ]
           [ '@' <@variable> ]

include ::= include <s> [ '[' <s> = <@index>
                        { ',' <s> = <@index> } ']' ]

for      ::= <@formula> for     <s> = <@index>
conRange ::= <@formula> foreach <s> = <@index> '..' <@index>
disRange ::= <@formula> forany  <s> = <@index> '..' <@index>

if ::= if <@index> = <@index> then <@formula>
                                else <@formula> endif

default ::= <@formula> for <s> = <@index>
            if not otherwise specified
\end{lstlisting}
\end{figure}

\begin{figure}[p]
\begin{lstlisting}[language=BNF,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=36]
collect ::= collect <s> <n> <@formula>

atom ::= <IntegerType> | <FloatType>
 | <VectorType> | <PointerType>
 | <Unused> | <IntZero> | <FloatZero>
 | <Constant> | <Preexecution> | <Argument> | <Instruction>
 | <Same> | <Distinct>
 | <DFGEdge> | <CFGEdge> | <CDGEdge> | <PDGEdge>
 | <FirstOperand> | <SecondOperand>
 | <ThirdOperand> | <FourthOperand>
 | <FirstSuccessor> | <SecondSuccessor>
 | <ThirdSuccessor> | <FourthSuccessor>
 | <Dominate> | <DominateStrict>
 | <Postdom> | <PostdomStrict> | <Blocked>
 | <IncomingValue> | <FunctionAttribute>
 | <Opcode> | <GeneralizedSame>

IntegerType ::= data_type <@variable> = integer
FloatType   ::= data_type <@variable> = float
VectorType  ::= data_type <@variable> = vector
PointerType ::= data_type <@variable> = pointer

Unused ::= <@variable> is unused
Opcode ::= opcode <@variable> = <s>

IntZero   ::= <@variable> is integer zero
FloatZero ::= <@variable> is floating point zero

Constant     ::= ir_type <@variable> = constant
Preexecution ::= ir_type <@variable> = preexecution
Argument     ::= ir_type <@variable> = argument
Instruction  ::= ir_type <@variable> = instruction

Same     ::= <@variable> = <@variable>
Distinct ::= <@variable> '≠' <@variable>

DFGEdge ::= <@variable> '∈' <@variable> '.' args
CFGEdge ::= <@variable> '∈' <@variable> '.' successors
CDGEdge ::= <@variable> has control dominance to <@variable>
PDGEdge ::= <@variable> has dependence edge to <@variable>

FirstOperand    ::= <@variable> =
                    <@variable> '.' args '[' 0 ']'
SecondOperand   ::= <@variable> =
                    <@variable> '.' args '[' 1 ']'
ThirdOperand    ::= <@variable> =
                    <@variable> '.' args '[' 2 ']'
FourthOperand   ::= <@variable> =
                    <@variable> '.' args '[' 3 ']'
\end{lstlisting}
\end{figure}

\begin{figure}[p]
\begin{lstlisting}[language=BNF,basicstyle=\linespread{0.8}\ttfamily,
                   firstnumber=86]
FirstSuccessor  ::= <@variable> =
                    <@variable> '.' successors '[' 0 ']'
SecondSuccessor ::= <@variable> =
                    <@variable> '.' successors '[' 1 ']'
ThirdSuccessor  ::= <@variable> =
                    <@variable> '.' successors '[' 2 ']'
FourthSuccessor ::= <@variable> =
                    <@variable> '.' successors '[' 3 ']'

Dominate       ::= domination
                   '(' <@variable> ',' <@variable> ')'
Postdom        ::= post_domination
                   '(' <@variable> ',' <@variable> ')'
DominateStrict ::= strict_domination
                   '(' <@variable> ',' <@variable> ')'
PostdomStrict  ::= strict_post_domination
                   '(' <@variable> ',' <@variable> ')'

IncomingValue ::= <@variable> '->' <@variable> 'Φ' <@variable>

Blocked ::= all control flow from <@variable> to <@variable>
            passes through <@variable>

FunctionAttribute ::= <@variable> has attribute pure

GeneralizedSame ::= <@variable> is the same set as <@variable>

@variable ::= <slottuple> | '{' <@openslot> '}'

slottuple ::= '{' <@openslot> ',' <@openslot>
                            { ',' <@openslot> } '}'

@openslot ::= <slotmember> | <slotindex> | <slotrange>
            | <slottuple>  | <slotbase>

slotbase   ::= <s>
slotmember ::= <@openslot> '.' <s>
slotindex  ::= <@openslot> '[' <@index> ']'
slotrange  ::= <@openslot> '[' <@index> '..' <@index> ']'

@index ::= <basevar> | <baseconst>
 | <addvar> | <addconst> | <subvar> | <subconst>

basevar   ::= <s>
baseconst ::= <n>
addvar    ::= <@index> '+' <s>
addconst  ::= <@index> '+' <n>
subvar    ::= <@index> '-' <s>
subconst  ::= <@index> '-' <n>
\end{lstlisting}
\end{figure}